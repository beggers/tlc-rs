// ===================
// Silent parsed rules
// ===================
//
// These are rules which need to be parsed for file correctness but don't need
// to be tokenized or otherwise used in the AST.

// TLA+ is sensitive enough to newlines that it feels like a bad idea
// to add them here. Only tabs and spaces.
WHITESPACE = _{ " " | "\t" }

single_line = _{ "----" ~ "-"* }
double_line = _{ "====" ~ "="* }
def_eq = _{ "==" }
mod_decl = _{ "MODULE" }

// ===================
// Literals
// ===================

nat_lit = @{ ASCII_DIGIT+ }
binary_lit = @{ ^"\\b" ~ ( "0" | "1" )+ }
octal_lit = @{ ^"\\o" ~ ( '0'..'7' )+ }
hex_lit = @{ ^"\\h" ~ ( ASCII_DIGIT | 'a'..'f' | 'A'..'F' )+ }
real_lit = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
number_lit = { real_lit | nat_lit | binary_lit | octal_lit | hex_lit }

nat_numbers_lit = { "Nat" }
int_numbers_lit = { "Int" }
real_numbers_lit = { "Real" }
number_set_lit = { nat_numbers_lit | int_numbers_lit | real_numbers_lit }

string_lit = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

// ===================
// Other things (TODO sort)
// ===================

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT)* }

expr = {
  string_lit | number_lit | number_set_lit
}

op_defn = {
  ident ~ def_eq ~ expr ~ NEWLINE
}

mod = {
  NEWLINE* ~ single_line ~ mod_decl ~ ident ~ single_line ~ NEWLINE
  ~ op_defn*
  ~ double_line ~ NEWLINE*
}

source_file = { mod+ }